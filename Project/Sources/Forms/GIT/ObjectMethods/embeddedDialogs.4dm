var $e:=FORM Event:C1606

If ($e.code<0)
	
	var $this : Object:=OBJECT Get value:C1743(OBJECT Get name:C1087).me.instance.data
	
	// MARK:-Specific actions
	var $form : cs:C1710._GIT_Controller:=formGetInstance
	var $git : cs:C1710.Git:=$form.Git
	
	Case of 
			
			//‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç
		: ($this.CANCELLED)
			
			// <NOTHING MORE TO DO>
			
			//‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç
		: (Bool:C1537($this.push))
			
			If ($git.remotes.length=0)
				
				var $gh:=cs:C1710.gh.me
				
				If (Not:C34($gh.available))
					
					$form.onDialogAlert({\
						title: $gh.lastError; \
						additional: "Installation instructions can be found at:\n\nhttps://github.com/cli/cli#installation"})
					
					//$form.alertDialog.show({\
						title: $gh.lastError; \
						additional: "Installation instructions can be found at:\n\nhttps://github.com/cli/cli#installation"})
					
					return   // üìå Avoid executing code that follows
					
				End if 
				
				If (Not:C34($gh.login()))
					
					return 
					
				End if 
				
				// Create remote
				var $remote:=$gh.createRepo($git.workspace.name)
				
				// Add the remote
				If ($git.execute("remote add -m -t origin "+$remote))
					
					$git.addRemote($git.currentBranch; $remote)
					$git.push("origin"; $git.currentBranch)
					
				End if 
				
			Else 
				
				If ($this.force)
					
					$git.forcePush()
					
				Else 
					
					$git.push()
					
				End if 
			End if 
			
			If ($git.success)
				
				$form.onActivate()
				
			Else 
				
				$form.onDialogAlert({\
					title: "Error"; \
					additional: $git.error})
				
				return   // üìå Avoid executing code that follows
				
			End if 
			
			//‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç
		: (Bool:C1537($this.pull))
			
			Form:C1466.stash:=Bool:C1537($this.stash)
			
			$git.pull(Bool:C1537($this.rebase); Bool:C1537($this.stash))
			
			RELOAD PROJECT:C1739
			
			$form.onActivate()
			
			//‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç
		: (Bool:C1537($this.checkout))
			
			$form.checkout:={\
				noChange: $this.noChange; \
				stash: $this.stash; \
				discard: $this.discard}
			
			Case of 
					
					// ______________________________________________________
				: ($form.checkout.noChange)
					
					$git.stash("save"; "4DPop autostash "+String:C10(Current date:C33; Internal date long:K1:5)+" at "+String:C10(Current time:C178; HH MM:K7:2))
					var $success:=$git.checkout($this.branch).success
					
					// ______________________________________________________
				: ($form.checkout.stash)
					
					$form.autostash:=True:C214
					
					$git.stash("save")
					$success:=$git.checkout($this.branch).success
					$git.stash("pop")
					
					// ______________________________________________________
				: ($form.checkout.discard)
					
					$form.Discard(Form:C1466.unstaged)
					$success:=$git.checkout($this.branch).success
					
					// ______________________________________________________
			End case 
			
			RELOAD PROJECT:C1739
			
			$form.onActivate()
			
			If (Not:C34($success))
				
				$form.onDialogAlert({main: "Git encountered an error"; additional: $git.error})
				
			End if 
			
			//‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç
		: (Bool:C1537($this.newBranch))
			
			Case of 
					
					// ______________________________________________________
				: (Length:C16(String:C10($this.branch))=0)
					
					BEEP:C151
					
					// ______________________________________________________
				: ($form.checkout.noChange)
					
					$git.stash("save"; "4DPop autostash "+String:C10(Current date:C33; Internal date long:K1:5)+" at "+String:C10(Current time:C178; HH MM:K7:2))
					$success:=$git.branch($this.checkout ? "createAndUse" : "create"; $this.branch).success
					
					// ______________________________________________________
				: ($form.checkout.stash)
					
					$form.autostash:=True:C214
					
					$git.stash("save")
					$success:=$git.branch($this.checkout ? "createAndUse" : "create"; $this.branch).success
					$git.stash("pop")
					
					// ______________________________________________________
				: ($form.checkout.discard)
					
					$form.Discard(Form:C1466.unstaged)
					$success:=$git.branch($this.checkout ? "createAndUse" : "create"; $this.branch).success
					
					// ______________________________________________________
			End case 
			
			RELOAD PROJECT:C1739
			
			$form.onActivate()
			
			If (Not:C34($success))
				
				$form.onDialogAlert({main: "Git encountered an error"; additional: $git.error})
				
			End if 
			
			//‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç‚ïç
	End case 
	
	// MARK:-Standard actions
	$this.me.hide()
	
	return 
	
End if 

// MARK:-Standard (positive) form events

//
